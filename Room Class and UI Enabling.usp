#HELP_BEGIN
Module accepts push events for setting room classification.
Sets analog_output "roomClass" used by routing modules for
permissions and handles UI button enabling for both
sources (based on selected room class) and destinations (based
on selected source class and type).
#HELP_END

#DEFINE_CONSTANT sbu 1
#DEFINE_CONSTANT sec 2
#DEFINE_CONSTANT coal 3
#DEFINE_CONSTANT ts 4

#DEFINE_CONSTANT camera 3
#DEFINE_CONSTANT workstation 1

#DEFINE_CONSTANT classNameSBU "UNCLASSIFIED"
#DEFINE_CONSTANT classNameSEC "US SECRET"
#DEFINE_CONSTANT classNameCOAL "COALITION SECRET"
#DEFINE_CONSTANT classNameTS "TS/SCI"

DIGITAL_INPUT
selectNone,
selectSBU,
selectSEC,
selectCOAL,
selectTS;

ANALOG_INPUT
selectedSourceClass,
selectedSourceType;

DIGITAL_OUTPUT
enableSBUnonVTCsource,
enableSECnonVTCsource,
enableCOALnonVTCsource,
enableTSnonVTCsource,
_skip_,
enableSBUvtcSource,
enableSECvtcSource,
enableCOALvtcSource,
enableTSvtcSource,
_skip_,
enableSBUvtcCamDests,
enableSECvtcCamDests,
enableCOALvtcCamDests,
enableTSvtcCamDests,
_skip_,
enableSBUvtcContentDests,
enableSECvtcContentDests,
enableCOALvtcContentDests,
enableTSvtcContentDests;

ANALOG_OUTPUT
_skip_,
roomClass;

STRING_OUTPUT
roomClassText;

FUNCTION setEnables()
{
	//enable non-VTC sources
	enableSBUnonVTCSource = (roomClass >= sbu);
	enableSECnonVTCSource = (roomClass >= sec);
	enableCOALnonVTCSource = (roomClass >= coal);
	enableTSnonVTCSource = (roomClass >= ts);

	//enable VTC sources
    enableSBUvtcSource = (roomClass = sbu);
	enableSECvtcSource = (roomClass = sec);
	enableCOALvtcSource = (roomClass = coal);
	enableTSvtcSource = (roomClass = ts);

	//enable VTC camera destinations
	enableSBUvtcCamDests = (roomClass = sbu && selectedSourceType = camera);
	enableSECvtcCamDests = (roomClass = sec && selectedSourceType = camera);
	enableCOALvtcCamDests = (roomClass = coal && selectedSourceType = camera);
	enableTSvtcCamDests = (roomClass = ts && selectedSourceType = camera);

	//enable VTC content destinations ("1<= selectedSourceClass..." prevents stateless sources (class 0) from routing to VTC)
	enableSBUvtcContentDests = (roomClass = sbu && selectedSourceType = workstation && selectedSourceClass = sbu);
    enableSECvtcContentDests = (roomClass = sec && selectedSourceType = workstation && 1 <= selectedSourceClass <= sec);
	enableCOALvtcContentDests = (roomClass = coal && selectedSourceType = workstation && 1 <= selectedSourceClass <= coal);
	enableTSvtcContentDests = (roomClass = ts && selectedSourceType = workstation && selectedSourceClass >= 1);
}

PUSH selectNone
{
	roomClass = 0;
	roomClassText = "Vacant";
	setEnables();
}

PUSH selectSBU
{
	roomClass = sbu;
	roomClassText = classNameSBU;
	setEnables();
}

PUSH selectSEC
{
	roomClass = sec;
	roomClassText = classNameSEC;
	setEnables();
}

PUSH selectCOAL
{
	roomClass = coal;
	roomClassText = classNameCOAL;
	setEnables();
}

PUSH selectTS
{
	roomClass = ts;
	roomClassText = classNameTS;
	setEnables();
}

CHANGE selectedSourceClass
{
	setEnables();
}

CHANGE selectedSourceType
{
	setEnables();
}
